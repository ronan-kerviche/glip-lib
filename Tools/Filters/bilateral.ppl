/*
	Bilateral Filter
	See http://en.wikipedia.org/wiki/Bilateral_filter
*/

REQUIRED_FORMAT:outputBilateralFormat(inputFormat0, *, *, *, *, GL_LINEAR, GL_LINEAR )

INCLUDE(maths.ppl)

SOURCE:bilateralShader
{
	#version 130
	uniform sampler2D inputBilateral;
	out vec4 outputBilateral;
	
	uniform float 	rI = 0.270,
			rL = 1.71;
	uniform int	windowSize = 5;
	
	// For the gaussian kernel : 
	#pragma INSERT(maths)

	void main()
	{
		int nWindow = windowSize / 2;
		vec4 	outCol = vec4(0.0, 0.0, 0.0, 0.0),
			refCol = texelFetch(inputBilateral, ivec2(gl_FragCoord.xy), 0);

		float nrm = 0.0;

		// Convolution : 
		for(int i=-nWindow; i<=nWindow; i++)
		{
			for(int j=-nWindow; j<=nWindow; j++)
			{
				vec4 	col 	= texelFetch(inputBilateral, ivec2(gl_FragCoord.xy + vec2(j, i)), 0);
				float 	a 	= gaussian( distance(col, refCol), rI ),
					b	= gaussian( length(vec2(j, i)), rL );
				outCol += col * a * b;
				nrm += a * b;
			}
		}
		outputBilateral = outCol / nrm;
	}
}

FILTER_LAYOUT:bilateralFilter(outputBilateralFormat,bilateralShader)

PIPELINE_MAIN:bilateralPipeline
{
	INPUT_PORTS(inputBilateral)
	OUTPUT_PORTS(outputBilateral)

	FILTER_INSTANCE:bilateralFilter
}
